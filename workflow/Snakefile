#!/usr/bin/env python3


include: "rules/common.smk"
include: "rules/samtools_import.smk"
include: "rules/samtools_fasta.smk"


# this is from the bpa_dataportal_downloads project
# results of a search for query = {"sample_id": '102.100.100/411655'}
data_file_dict = {
    "396015_PP_BRF_m84118_250606_030322_s1.hifi_reads.bc2040.bam": "https://data.bioplatforms.com/dataset/bpa-pp-pacbio-hifi-396015-m84118_250606_030322_s1/resource/cae55399b815df4ebb44c7c0abaaa227/download/396015_PP_BRF_m84118_250606_030322_s1.hifi_reads.bc2040.bam",
    "396015_LibID398420_PP_BRF_225MWJLT1_CAGTGACG_S4_L002_R1_001.fastq.gz": "https://data.bioplatforms.com/dataset/bpa-pp-illumina-shortread-396015-225mwjlt1/resource/14d9ec4fbc39b1c74716f7fed2280e26/download/396015_LibID398420_PP_BRF_225MWJLT1_CAGTGACG_S4_L002_R1_001.fastq.gz",  
    "396015_LibID398420_PP_BRF_225MWJLT1_CAGTGACG_S4_L002_R2_001.fastq.gz": "https://data.bioplatforms.com/dataset/bpa-pp-illumina-shortread-396015-225mwjlt1/resource/3a3d7ae18e1e98efaf644ad25f420afe/download/396015_LibID398420_PP_BRF_225MWJLT1_CAGTGACG_S4_L002_R2_001.fastq.gz",
    "396015_LibID398420_PP_BRF_225MWJLT1_CAGTGACG_S4_L001_R2_001.fastq.gz": "https://data.bioplatforms.com/dataset/bpa-pp-illumina-shortread-396015-225mwjlt1/resource/82cfacab63e28af376997c287a4c8c30/download/396015_LibID398420_PP_BRF_225MWJLT1_CAGTGACG_S4_L001_R2_001.fastq.gz",
    "396015_LibID398420_PP_BRF_225MWJLT1_CAGTGACG_S4_L001_R1_001.fastq.gz": "https://data.bioplatforms.com/dataset/bpa-pp-illumina-shortread-396015-225mwjlt1/resource/4f177e76eb7298f1c3afa4ce8661cb32/download/396015_LibID398420_PP_BRF_225MWJLT1_CAGTGACG_S4_L001_R1_001.fastq.gz",
}


rule format_config_file:
    input:
        sanger_config_template=local(sanger_config_template),
    output:
        rendered_yaml=add_bucket_to_path(
            Path(
                dataset_id, "results", "config", "sangertol_genomeassembly_params.yaml"
            )
        ),
    params:
        pacbio_reads=[rules.samtools_fasta.output.reads],
        hic_reads=[rules.samtools_import.output.cram],
    container:
        get_container("ncbi-datasets-pylib")
    script:
        "scripts/format_config_file.py"


rule config_target:
    default_target: True
    localrule: True
    input:
        storage.s3(rules.samtools_import.output),
        storage.s3(rules.samtools_fasta.output),
        rendered_yaml=storage.s3(rules.format_config_file.output.rendered_yaml),
    output:
        "results/sangertol_genomeassembly_params.yaml",
    shell:
        "cp {input.rendered_yaml} {output}"


module test_rm:
    snakefile:
        github("tomharrop/test-module-rm", path="workflow/Snakefile", tag="v0.0.6")
    config:
        {
            **config,
            "query_genome": storage.s3(
                "s3://pawsey1132.atol.testassembly/414129_AusARG/results/sanger_tol/414129.hifiasm.20250123/scaffolding/yahs/out.break.yahs/out_scaffolds_final.fa",
            ),
        }


use rule * from test_rm as test_rm_*


use rule clean from test_rm as test_rm_clean with:
    resources:
        runtime=lambda wildcards, attempt: 720 * attempt,
        mem_mb=lambda wildcards, attempt: 48e3 * attempt,


use rule all from test_rm as rm_all with:
    output:
        storage.s3(
            "s3://pawsey1132.atol.testassembly/414129_AusARG/results/out_scaffolds_final.masked.fa.gz",
        ),
