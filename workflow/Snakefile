#!/usr/bin/env python3


include: "rules/common.smk"
include: "rules/samtools_import.smk"
include: "rules/samtools_fasta.smk"


# this is from the bpa_dataportal_downloads project
# results of a search for query = {"sample_id": '102.100.100/411655'}
data_file_dict = {
    "611522_AVIAN_AGRF_m84073_250321_080406_s1.ccs.bam": "https://data.bioplatforms.com/dataset/bpa-avian-pacbio-hifi-611522-m84073_250321_080406_s1/resource/a4fe772b31af6c0d63482d8ea5f65bef/download/611522_AVIAN_AGRF_m84073_250321_080406_s1.ccs.bam",
    "611531_AVIAN_BRF_22KYTHLT4_CCACAACA_S15_L002_R1_001.fastq.gz": "https://data.bioplatforms.com/dataset/bpa-avian-hi-c-611531-22kythlt4/resource/3b72b0a25ee0d9476542e62d3cebfc61/download/611531_AVIAN_BRF_22KYTHLT4_CCACAACA_S15_L002_R1_001.fastq.gz",
    "611531_AVIAN_BRF_22KYTHLT4_CCACAACA_S15_L002_R2_001.fastq.gz": "https://data.bioplatforms.com/dataset/bpa-avian-hi-c-611531-22kythlt4/resource/9d6906bc936146cad8ed60388c5d4f55/download/611531_AVIAN_BRF_22KYTHLT4_CCACAACA_S15_L002_R2_001.fastq.gz",
}


rule format_config_file:
    input:
        sanger_config_template=local(sanger_config_template),
    output:
        rendered_yaml=add_bucket_to_path(
            Path(
                dataset_id, "results", "config", "sangertol_genomeassembly_params.yaml"
            )
        ),
    params:
        pacbio_reads=[rules.samtools_fasta.output.reads],
        hic_reads=[rules.samtools_import.output.cram],
    container:
        get_container("ncbi-datasets-pylib")
    script:
        "scripts/format_config_file.py"


rule config_target:
    default_target: True
    localrule: True
    input:
        storage.s3(rules.samtools_import.output),
        storage.s3(rules.samtools_fasta.output),
        rendered_yaml=storage.s3(rules.format_config_file.output.rendered_yaml),
    output:
        "results/sangertol_genomeassembly_params.yaml",
    shell:
        "cp {input.rendered_yaml} {output}"


module test_rm:
    snakefile:
        github("tomharrop/test-module-rm", path="workflow/Snakefile", tag="v0.0.6")
    config:
        {
            **config,
            "query_genome": storage.s3(
                "s3://pawsey1132.atol.testassembly/414129_AusARG/results/sanger_tol/414129.hifiasm.20250123/scaffolding/yahs/out.break.yahs/out_scaffolds_final.fa",
            ),
        }


use rule * from test_rm as test_rm_*


use rule clean from test_rm as test_rm_clean with:
    resources:
        runtime=lambda wildcards, attempt: 720 * attempt,
        mem_mb=lambda wildcards, attempt: 48e3 * attempt,


use rule all from test_rm as rm_all with:
    output:
        storage.s3(
            "s3://pawsey1132.atol.testassembly/414129_AusARG/results/out_scaffolds_final.masked.fa.gz",
        ),
